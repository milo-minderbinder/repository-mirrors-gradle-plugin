plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'pl.allegro.tech.build.axion-release' version '1.10.2'
}

description 'A Gradle init script plugin that dynamically updates buildscript and project repositories to use the configured mirror URLs.'
group 'co.insecurity'
scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, c -> /\b$v\b/}, replacement: {v, c -> /$v/}]
        pre 'commit'
    }
}
project.version = scmVersion.version
logger.info("version: ${project.version}")

apply from: "${rootDir}/gradle/functional-test.gradle"
apply from: "${rootDir}/gradle/maven-publish.gradle"

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile localGroovy()
    testCompile gradleTestKit()
    testCompile("org.spockframework:spock-core:1.3-groovy-2.5") {
        exclude module: "groovy-all"
    }
    testCompile 'org.mock-server:mockserver-netty:5.7.0'
    testRuntime files(pluginUnderTestMetadata)
}

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

gradlePlugin {
    plugins {
        repositoryMirrorsPlugin {
            id = 'co.insecurity.repository-mirrors'
            implementationClass = 'co.insecurity.gradle.repository_mirrors.RepositoryMirrorsPlugin'
        }
    }
}

pluginBundle {
    website = "https://github.com/milo-minderbinder/${rootProject.name}"
    vcsUrl = "https://github.com/milo-minderbinder/${rootProject.name}.git"
    tags = ['repository', 'mirror', 'mirrors', 'artifactory']
    plugins {
        repositoryMirrorsPlugin {
            displayName = 'Repository Mirrors Gradle Plugin'
            description = "${rootProject.description}"
        }
    }
}
