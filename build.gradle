plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'pl.allegro.tech.build.axion-release' version '1.17.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor = JvmVendorSpec.AZUL
    }
}

description 'A Gradle init script plugin that dynamically updates buildscript and project repositories to use the configured mirror URLs.'
group 'co.insecurity'
scmVersion {
    ignoreUncommittedChanges = false
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    versionIncrementer 'incrementPatch'
    branchVersionIncrementer = [
            'feature/.*': 'incrementMinor',
            'bugfix/.*': 'incrementPatch',
    ]
    createReleaseCommit false
    hooks {
        pre 'fileUpdate', [
                files: ['README.md'],
                pattern: {v, c -> /\b$v\b/},
                replacement: {v, c -> "$v"},
        ]
        pre 'commit'
    }
}
project.version = scmVersion.version
logger.info("version: ${project.version}")

apply from: "${rootDir}/gradle/functional-test.gradle"
apply from: "${rootDir}/gradle/maven-publish.gradle"

repositories {
    mavenCentral()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'skipped', 'failed'
    }
}

dependencies {
    implementation localGroovy()
    implementation gradleApi()

    testImplementation platform('org.spockframework:spock-bom:2.3-groovy-3.0')
    testImplementation 'org.spockframework:spock-core'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    functionalTestImplementation 'org.mock-server:mockserver-netty:5.15.0'
}

gradlePlugin {
    website = "https://github.com/milo-minderbinder/${rootProject.name}"
    vcsUrl = "https://github.com/milo-minderbinder/${rootProject.name}.git"
    plugins {
        repositoryMirrorsPlugin {
            id = 'co.insecurity.repository-mirrors'
            implementationClass = 'co.insecurity.gradle.repository_mirrors.RepositoryMirrorsPlugin'
            displayName = 'Repository Mirrors Gradle Plugin'
            description = "${rootProject.description}"
            tags.set(['repository', 'mirror', 'mirrors', 'artifactory'])
        }
    }
}

