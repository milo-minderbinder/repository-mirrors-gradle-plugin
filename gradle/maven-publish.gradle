afterEvaluate {
    plugins.withType(JavaPlugin) {
        task sourcesJar(type: Jar) {
            archiveClassifier = 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            dependsOn javadoc
            archiveClassifier = 'javadoc'
            from javadoc.destinationDir
        }
    }

    plugins.withType(GroovyPlugin) {
        task groovydocJar(type: Jar) {
            dependsOn groovydoc
            archiveClassifier = 'groovydoc'
            from groovydoc.destinationDir
        }
    }

    artifacts {
        plugins.withType(JavaPlugin) {
            archives sourcesJar
            archives javadocJar
        }

        plugins.withType(GroovyPlugin) {
            archives groovydocJar
        }
    }

    publishing {
        publications {
            pluginMaven(MavenPublication) {
                plugins.withType(JavaPlugin) {
                    artifact sourcesJar
                    artifact javadocJar
                }
                plugins.withType(GroovyPlugin) {
                    artifact groovydocJar
                }

                pom {
                    description = "${rootProject.description}"
                    url = "https://github.com/milo-minderbinder/${rootProject.name}"

                    scm {
                        connection = "git@github.com:milo-minderbinder/${rootProject.name}.git"
                        developerConnection = "git@github.com:milo-minderbinder/${rootProject.name}.git"
                        url = "https://github.com/milo-minderbinder/${rootProject.name}"
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'milo-minderbinder'
                            name = 'Milo Minderbinder'
                            email = 'minderbinder.enterprises@gmail.com'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'testRepo'
                url = "${rootProject.buildDir}/test-repo"
            }
        }
    }

    //signing {
    //    useGpgCmd()
    //    sign publishing.publications.matching {true}
    //}
}
